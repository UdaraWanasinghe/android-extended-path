apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

dokkaHtml {
    pluginsMapConfiguration.set(
            [
                    "org.jetbrains.dokka.base.DokkaBase": """{"footerMessage": "Copyright Â© 2022 AureusApps"}"""
            ]
    )
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaHtml) {
    archiveClassifier.set('javadoc')
    from dokkaHtml.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // standard metadata
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // publication component defined by the android library plugin
                from components.release

                // custom artifacts
                artifact androidSourcesJar
                artifact javadocJar

                // pom file
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Android graphics path with extended functionalities.'
                    url = 'https://github.com/UdaraWanasinghe/android-extended-path'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/UdaraWanasinghe/android-extended-path/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'UdaraWanasinghe'
                            name = 'Udara Wanasinghe'
                            email = 'udara.developer@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/UdaraWanasinghe/android-extended-path.git'
                        developerConnection = 'scm:git:ssh://github.com/UdaraWanasinghe/android-extended-path.git'
                        url = 'https://github.com/UdaraWanasinghe/android-extended-path'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}